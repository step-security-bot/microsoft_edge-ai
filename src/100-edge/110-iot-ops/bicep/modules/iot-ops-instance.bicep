metadata name = 'IoT Operations Instance Module'
metadata description = 'Deploys Azure IoT Operations instance, broker, authentication, listeners, and data flow components on an Azure Arc-enabled Kubernetes cluster.'

import * as core from '../types.core.bicep'
import * as types from '../types.bicep'

/*
  Common Parameters
*/

@description('The common component configuration.')
param common core.Common

@description('Name of the existing arc-enabled cluster where AIO will be deployed.')
param arcConnectedClusterName string

/*
  Azure IoT Operations Extension Parameters
*/

@description('The name for the Azure IoT Operations Instance resource.')
param aioInstanceName string

@description('The resource name for the User Assigned Identity for Azure IoT Operations.')
param aioIdentityName string

@description('The settings for the Azure IoT Operations Extension.')
param aioExtensionConfig types.AioExtension

param aioFeatures types.AioFeatures?

/*
  Secret Sync Parameters
*/

@description('The name of the User Assigned Managed Identity for Secret Sync.')
param sseIdentityName string

@description('The name of the Key Vault for Secret Sync.')
param sseKeyVaultName string

/*
  Security and Trust Parameters
*/

@description('The resource ID for the Secret Store Extension.')
#disable-next-line secure-secrets-in-params
param secretStoreExtensionId string

@description('The source for trust for Azure IoT Operations.')
param trustSource types.TrustSource

@description('The trust settings for Azure IoT Operations.')
param trustIssuerSettings types.TrustSettingsConfig?

/*
  Integration Parameters
*/

@description('The resource name for the ADR Schema Registry for Azure IoT Operations.')
param schemaRegistryName string

@description('The resource ID for the ADR Namespace for Azure IoT Operations.')
param adrNamespaceId string?

@description('Whether or not to enable the Open Telemetry Collector for Azure IoT Operations.')
param shouldEnableOtelCollector bool

/*
  Messaging Parameters
*/

@description('Configuration for the insecure anonymous AIO MQ Broker Listener.')
param brokerListenerAnonymousConfig types.AioMqBrokerAnonymous

@description('The settings for the Azure IoT Operations MQ Broker.')
param aioMqBrokerConfig types.AioMqBroker

@description('Whether to enable an insecure anonymous AIO MQ Broker Listener. (Should only be used for dev or test environments)')
param shouldCreateAnonymousBrokerListener bool = false

/*
  Data Flow Parameters
*/

@description('The settings for Azure IoT Operations Data Flow Instances.')
param aioDataFlowInstanceConfig types.AioDataFlowInstance

/*
  Custom Location Parameters
*/

@description('The name for the Custom Locations resource.')
param customLocationName string

@description('Whether or not to deploy the Custom Locations Resource Sync Rules for the Azure IoT Operations resources.')
param shouldDeployResourceSyncRules bool

/*
  Variables
*/

var metrics = {
  enabled: shouldEnableOtelCollector
  otelCollectorAddress: shouldEnableOtelCollector
    ? 'aio-otel-collector.${aioExtensionConfig.settings.namespace}.svc.cluster.local:4317'
    : ''
  exportIntervalSeconds: 60
}

// For now, only support self signed cert and trust until customer managed has been implemented
var selfSignedIssuerName = '${aioExtensionConfig.settings.namespace}-aio-certificate-issuer'
var selfSignedConfigMapName = '${aioExtensionConfig.settings.namespace}-aio-ca-trust-bundle'
var trust = trustIssuerSettings ?? {
  issuerName: selfSignedIssuerName
  issuerKind: 'ClusterIssuer'
  configMapName: selfSignedConfigMapName
  configMapKey: ''
}

var aioMqBrokerAddress = 'mqtts://${aioMqBrokerConfig.brokerListenerServiceName}.${aioExtensionConfig.settings.namespace}:${aioMqBrokerConfig.brokerListenerPort}'

/*
  Resources
*/

resource schemaRegistry 'Microsoft.DeviceRegistry/schemaRegistries@2025-10-01' existing = {
  name: schemaRegistryName
}

resource arcConnectedCluster 'Microsoft.Kubernetes/connectedClusters@2024-12-01-preview' existing = {
  name: arcConnectedClusterName
}

resource aioExtension 'Microsoft.KubernetesConfiguration/extensions@2023-05-01' = {
  scope: arcConnectedCluster
  name: 'azure-iot-operations-${take(uniqueString(arcConnectedCluster.id), 5)}'
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    extensionType: 'microsoft.iotoperations'
    version: aioExtensionConfig.release.version
    releaseTrain: aioExtensionConfig.release.train
    autoUpgradeMinorVersion: false
    scope: {
      cluster: {
        releaseNamespace: aioExtensionConfig.settings.namespace
      }
    }
    configurationSettings: {
      #disable-next-line prefer-unquoted-property-names
      'AgentOperationTimeoutInMinutes': any(aioExtensionConfig.settings.agentOperationTimeoutInMinutes)
      'connectors.values.mqttBroker.address': aioMqBrokerAddress
      'connectors.values.mqttBroker.serviceAccountTokenAudience': aioMqBrokerConfig.serviceAccountAudience
      'connectors.values.opcPlcSimulation.deploy': 'false'
      'connectors.values.opcPlcSimulation.autoAcceptUntrustedCertificates': 'false'
      'adr.values.Microsoft.CustomLocation.ServiceAccount': 'default'
      'akri.values.webhookConfiguration.enabled': 'false'
      'akri.values.certManagerWebhookCertificate.enabled': 'false'
      'akri.values.agent.extensionService.mqttBroker.hostName': '${aioMqBrokerConfig.brokerListenerServiceName}.${aioExtensionConfig.settings.namespace}'
      'akri.values.agent.extensionService.mqttBroker.port': any(aioMqBrokerConfig.brokerListenerPort)
      'akri.values.agent.extensionService.mqttBroker.serviceAccountAudience': aioMqBrokerConfig.serviceAccountAudience
      'akri.values.agent.host.containerRuntimeSocket': ''
      'akri.values.kubernetesDistro': toLower(aioExtensionConfig.settings.kubernetesDistro)
      'mqttBroker.values.global.quickstart': 'false'
      'mqttBroker.values.operator.firstPartyMetricsOn': 'true'
      'observability.metrics.enabled': '${metrics.enabled}'
      'observability.metrics.openTelemetryCollectorAddress': metrics.otelCollectorAddress
      'observability.metrics.exportIntervalSeconds': '${metrics.exportIntervalSeconds}'
      #disable-next-line prefer-unquoted-property-names
      'trustSource': trustSource
      'trustBundleSettings.issuer.name': trust.issuerName
      'trustBundleSettings.issuer.kind': trust.issuerKind
      'trustBundleSettings.configMap.name': trust.configMapName
      'trustBundleSettings.configMap.key': trust.configMapKey
      'schemaRegistry.values.mqttBroker.host': aioMqBrokerAddress
      'schemaRegistry.values.mqttBroker.tlsEnabled': any(true)
      'schemaRegistry.values.mqttBroker.serviceAccountTokenAudience': aioMqBrokerConfig.serviceAccountAudience
    }
  }
}

resource customLocation 'Microsoft.ExtendedLocation/customLocations@2021-08-31-preview' = {
  name: customLocationName
  location: common.location
  identity: {
    type: 'SystemAssigned'
  }
  properties: {
    hostResourceId: arcConnectedCluster.id
    namespace: aioExtensionConfig.settings.namespace
    displayName: customLocationName
    clusterExtensionIds: [secretStoreExtensionId, aioExtension.id]
  }
}

resource aioSyncRule 'Microsoft.ExtendedLocation/customLocations/resourceSyncRules@2021-08-31-preview' = if (shouldDeployResourceSyncRules) {
  parent: customLocation
  name: '${customLocationName}-broker-sync'
  location: common.location
  properties: {
    priority: 400
    selector: {
      matchLabels: {
        #disable-next-line no-hardcoded-env-urls
        'management.azure.com/provider-name': 'microsoft.iotoperations'
      }
    }
    targetResourceGroup: resourceGroup().id
  }
}

resource adrSyncRule 'Microsoft.ExtendedLocation/customLocations/resourceSyncRules@2021-08-31-preview' = if (shouldDeployResourceSyncRules) {
  parent: customLocation
  name: '${customLocationName}-adr-sync'
  location: common.location
  properties: {
    priority: 200
    selector: {
      matchLabels: {
        #disable-next-line no-hardcoded-env-urls
        'management.azure.com/provider-name': 'Microsoft.DeviceRegistry'
      }
    }
    targetResourceGroup: resourceGroup().id
  }
  dependsOn: [
    aioSyncRule
  ]
}

resource sseIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' existing = {
  name: sseIdentityName

  resource sseFedCred 'federatedIdentityCredentials' = {
    name: 'aio-sse-ficred'
    properties: {
      audiences: ['api://AzureADTokenExchange']
      issuer: arcConnectedCluster.properties.oidcIssuerProfile.issuerUrl
      subject: 'system:serviceaccount:${aioExtensionConfig.settings.namespace}:aio-ssc-sa'
    }
  }
}

resource aioIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' existing = {
  name: aioIdentityName

  resource aioFedCred 'federatedIdentityCredentials' = {
    name: 'aio-instance-ficred'
    properties: {
      audiences: ['api://AzureADTokenExchange']
      issuer: arcConnectedCluster.properties.oidcIssuerProfile.issuerUrl
      subject: 'system:serviceaccount:${aioExtensionConfig.settings.namespace}:aio-dataflow'
    }
  }
}

resource defaultSecretSyncSecretProviderClass 'Microsoft.SecretSyncController/azureKeyVaultSecretProviderClasses@2024-08-21-preview' = {
  name: 'spc-ops-${take(uniqueString('${arcConnectedClusterName}-${resourceGroup().name}-${aioInstanceName}'), 7)}'
  location: common.location

  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }

  properties: {
    clientId: sseIdentity.properties.clientId
    keyvaultName: sseKeyVaultName!
    tenantId: sseIdentity.properties.tenantId
  }
}

resource aioInstance 'Microsoft.IoTOperations/instances@2025-10-01' = {
  name: aioInstanceName
  location: common.location
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${aioIdentity.id}': {}
    }
  }
  properties: union(
    {
      description: 'An AIO instance.'
      schemaRegistryRef: {
        resourceId: schemaRegistry.id
      }
      defaultSecretProviderClassRef: {
        resourceId: defaultSecretSyncSecretProviderClass.id
      }
    },
    adrNamespaceId == null
      ? {}
      : {
          adrNamespaceRef: {
            resourceId: adrNamespaceId
          }
        },
    aioFeatures == null
      ? {}
      : {
          features: aioFeatures
        }
  )
}

resource registryEndpoint 'Microsoft.IoTOperations/instances/registryEndpoints@2025-10-01' = {
  parent: aioInstance
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    host: 'mcr.microsoft.com'
    authentication: {
      method: 'Anonymous'
      anonymousSettings: {}
    }
  }
}

resource broker 'Microsoft.IoTOperations/instances/brokers@2025-10-01' = {
  parent: aioInstance
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: union(
    {
      memoryProfile: aioMqBrokerConfig.memoryProfile
      generateResourceLimits: {
        cpu: 'Disabled'
      }
      cardinality: {
        backendChain: {
          partitions: aioMqBrokerConfig.backendPartitions
          workers: aioMqBrokerConfig.backendWorkers
          redundancyFactor: aioMqBrokerConfig.backendRedundancyFactor
        }
        frontend: {
          replicas: aioMqBrokerConfig.frontendReplicas
          workers: aioMqBrokerConfig.frontendWorkers
        }
      }
      diagnostics: {
        logs: {
          level: aioMqBrokerConfig.logsLevel
        }
      }
    },
    aioMqBrokerConfig.?persistence != null
      ? {
          persistence: aioMqBrokerConfig.persistence!
        }
      : {}
  )
}

resource brokerAuthn 'Microsoft.IoTOperations/instances/brokers/authentications@2025-10-01' = {
  parent: broker
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    authenticationMethods: [
      {
        method: 'ServiceAccountToken'
        serviceAccountTokenSettings: {
          audiences: [aioMqBrokerConfig.serviceAccountAudience]
        }
      }
    ]
  }
}

resource brokerListener 'Microsoft.IoTOperations/instances/brokers/listeners@2025-10-01' = {
  parent: broker
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    serviceType: aioMqBrokerConfig.serviceType
    serviceName: aioMqBrokerConfig.brokerListenerServiceName
    ports: [
      {
        authenticationRef: brokerAuthn.name
        port: aioMqBrokerConfig.brokerListenerPort
        tls: {
          mode: 'Automatic'
          certManagerCertificateSpec: {
            issuerRef: {
              name: trust.issuerName
              kind: trust.issuerKind
              group: 'cert-manager.io'
            }
          }
        }
      }
    ]
  }
}

resource brokerListenerAnonymous 'Microsoft.IoTOperations/instances/brokers/listeners@2025-10-01' = if (shouldCreateAnonymousBrokerListener) {
  parent: broker
  name: 'default-anon'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    serviceType: 'NodePort'
    serviceName: brokerListenerAnonymousConfig.serviceName
    ports: [
      {
        port: brokerListenerAnonymousConfig.port
        nodePort: brokerListenerAnonymousConfig.nodePort
      }
    ]
  }
  dependsOn: [
    brokerAuthn
  ]
}

resource dataFlowProfile 'Microsoft.IoTOperations/instances/dataflowProfiles@2025-10-01' = {
  parent: aioInstance
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    instanceCount: aioDataFlowInstanceConfig.count
  }
}

resource dataFlowEndpoint 'Microsoft.IoTOperations/instances/dataflowEndpoints@2025-10-01' = {
  parent: aioInstance
  name: 'default'
  extendedLocation: {
    name: customLocation.id
    type: 'CustomLocation'
  }
  properties: {
    endpointType: 'Mqtt'
    mqttSettings: {
      host: '${aioMqBrokerConfig.brokerListenerServiceName}:${aioMqBrokerConfig.brokerListenerPort}'
      authentication: {
        method: 'ServiceAccountToken'
        serviceAccountTokenSettings: {
          audience: aioMqBrokerConfig.serviceAccountAudience
        }
      }
      tls: {
        mode: 'Enabled'
        trustedCaCertificateConfigMapRef: trust.configMapName
      }
    }
  }
}

/*
  Outputs
*/

@description('The name of the deployed Azure IoT Operations Instance.')
output aioInstanceName string = aioInstance.name

@description('The ID of the deployed Azure IoT Operations Instance.')
output aioInstanceId string = aioInstance.id

@description('The name of the deployed Custom Location resource.')
output customLocationName string = customLocation.name

@description('The ID of the deployed Custom Location resource.')
output customLocationId string = customLocation.id

@description('The name of the deployed Azure IoT Operations Data Flow Profile.')
output dataFlowProfileName string = dataFlowProfile.name

@description('The ID of the deployed Azure IoT Operations Data Flow Profile.')
output dataFlowProfileId string = dataFlowProfile.id

@description('The name of the deployed Azure IoT Operations Data Flow Endpoint.')
output dataFlowEndpointName string = dataFlowEndpoint.name

@description('The ID of the deployed Azure IoT Operations Data Flow Endpoint.')
output dataFlowEndpointId string = dataFlowEndpoint.id

@description('The name of the deployed IoT Operations Extension.')
output aioExtensionName string = aioExtension.name

@description('The ID of the deployed IoT Operations Extension.')
output aioExtensionId string = aioExtension.id

@description('The name of the deployed Anonymous Broker Listener, if created.')
output brokerListenerAnonymousName string = shouldCreateAnonymousBrokerListener ? brokerListenerAnonymous.name : ''

@description('The ID of the deployed Anonymous Broker Listener, if created.')
output brokerListenerAnonymousId string = shouldCreateAnonymousBrokerListener ? brokerListenerAnonymous.id : ''

@description('The name of the deployed AIO Broker Sync Rule, if created.')
output aioSyncRuleName string = shouldDeployResourceSyncRules ? aioSyncRule.name : ''

@description('The ID of the deployed AIO Broker Sync Rule, if created.')
output aioSyncRuleId string = shouldDeployResourceSyncRules ? aioSyncRule.id : ''

@description('The name of the deployed ADR Sync Rule, if created.')
output adrSyncRuleName string = shouldDeployResourceSyncRules ? adrSyncRule.name : ''

@description('The ID of the deployed ADR Sync Rule, if created.')
output adrSyncRuleId string = shouldDeployResourceSyncRules ? adrSyncRule.id : ''

@description('The name of the deployed AIO MQ Broker.')
output brokerName string = broker.name

@description('The ID of the deployed AIO MQ Broker.')
output brokerId string = broker.id

@description('The name of the deployed AIO MQ Broker Authentication.')
output brokerAuthnName string = brokerAuthn.name

@description('The ID of the deployed AIO MQ Broker Authentication.')
output brokerAuthnId string = brokerAuthn.id

@description('The name of the deployed AIO MQ Broker Listener.')
output brokerListenerName string = brokerListener.name

@description('The ID of the deployed AIO MQ Broker Listener.')
output brokerListenerId string = brokerListener.id
